/*
 * JSFX Name: MIDI notes octave transpose shift
 * Author: X-Raym
 * Author URI: http://www.extremraym.com
 * Links:
     Forum Thread http://forum.cockos.com/showthread.php?t=181105
 * Donation: http://www.extremraym.com/en/donation
 * Licence: GPL v3
 * REAPER: 5.0
 * Version: 1.2.2
 */

/**
 * Changelog:
 * v1.2.2 (2022-12-26)
  # CPU Hog fix
 * v1.2.1 (2022-12-23)
  + Correct "Tranpose" by "Transpose"
 * v1.2 (2022-09-10)
  + Add all notes-off sends event if slider change
 * v1.1 (2019-07-01)
  + Increase range
 * v1.0 (2019-07-01)
  + Initial Release
 */

desc:MIDI Notes Octave Transpose Shift

slider1:0<0,16,1{Any,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Input Channel

slider5:0<-12,12,1>Octave Shift


////////////////////////////////////////////////////////////////////////////////
@init
statNoteOn = $x90;
statNoteOff = $x80;
afterTouch = $xA0;

last_chan = slider1;
last_octave = slider5;

// Thanks mschnell
allOff1   = 0xB0;
allOff2   = 0x7B;
allOff3   = 0x00;
holdOff1  = 0xB0;
holdOff2  = 0x40;
holdOff3  = 0x00;

function SendAllNotesOff() (
  outChannel = 0;
  opos = mpos;
  opos >= samplesblock-32 ? (
    opos = samplesblock-33;
  );
  loop (16,
    midisend(opos, holdOff1+outChannel, holdOff2, holdOff3);
    opos +=1;
    midisend(opos, allOff1+outChannel,  allOff2,  allOff3);
    opos +=1;
    outChannel += 1;
  );
);

////////////////////////////////////////////////////////////////////////////////
@slider
inChannel = slider1 - 1;

////////////////////////////////////////////////////////////////////////////////
@block

while
(

  // Not Off if Slider Change
  last_chan != slider1 || last_octave != slider5 ? (
    SendAllNotesOff();
    last_chan = slider1;
    last_octave = slider5;
  );

  midirecv(offset,msg1,note,vel) ?
  (

    // Extract message type and channel
    status = msg1 & $xF0;
    channel = msg1 & $x0F;

    // Is it on our channel?
    channel == inChannel || inChannel == -1 ?
    (
      
      // Is it a note event?
      status == statNoteOn || status == statNoteOff || status == afterTouch ?
      (
        
        
          slider5 != 0 ? (
            note = max(0,min(127,note + slider5*12)); // Get Note Out
          );

     
      );
    
    );
    
    midisend(offset, msg1, note, vel);
    
    1; // Force loop to continue until all messages have been processed
  
  );

);